source: flights_table is table('duckdb:data/flights.parquet')

query: plane_destinations is flights_table -> {
  group_by: tail_num
  # list
  nest: d1 is {
    group_by: destination
  }
  # list
  nest: d2 is {
    group_by: destination
  }
} 



source: planes is from(->plane_destinations) + {
  measure:
    plane_count is count()
    jfk_count is plane_count {
      where: d1.destination='JFK'
    }
    jfk_and_lax_count is plane_count {
      where: 
        d1.destination='JFK'
        and d2.destination='LAX'
    }
    jfk_and_not_lax_count is jfk_count - jfk_and_lax_count

  query: stats is {
    aggregate: 
      plane_count
      jfk_count
      jfk_and_lax_count
      jfk_and_not_lax_count
  }

  query: flew_to_jfk is {
    where: d1.destination='JFK'
    aggregate: 
      plane_count
    # list_detail
    nest: planes is {
      group_by: tail_num
    }
  }

  query: flew_to_lax is {
    where: d1.destination='LAX'
    aggregate: 
      plane_count
    # list_detail
    nest: planes is {
      group_by: tail_num
    }
  }
}